buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.ajoberstar.grgit', name: 'grgit-gradle', 'version': '4.1.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply from: 'utils/package-info.gradle'

version = getVersion()
println('Mod version: ' + version)
group = 'dev.socketmods.socketnukes'
archivesBaseName = "socketnukes-${mc_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
    data
    main.resources {
        srcDirs += data.resources.srcDirs
        exclude '.cache/'
    }
}

println('Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', logging_markers
            property 'forge.logging.console.level', logging_console

            ideaModule "${project.name}.main"
            mods {
                socketnukes {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', logging_markers
            property 'forge.logging.console.level', logging_console

            ideaModule "${project.name}.main"
            mods {
                socketnukes {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', logging_markers
            property 'forge.logging.console.level', logging_console

            args '--mod', 'socketnukes', '--all'
            args '--output', sourceSets.data.resources.srcDirs[0].toString()
            args '--existing', sourceSets.main.resources.srcDirs[0].toString()

            ideaModule "${project.name}.data"
            mods {
                socketnukes {
                    sources sourceSets.main, sourceSets.data
                }
            }
        }
    }
}

configurations {
    dataImplementation.extendsFrom implementation
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    dataImplementation sourceSets.main.output
}

def commit_id = "unknown"
def git_timestamp = "unknown"
try {
    commit_id = grgit.head().id
    def datetime = grgit.head().dateTime
    git_timestamp = datetime.toLocalDateTime().toString() + datetime.getOffset().toString().replace(':', '')
} catch (Exception e) {
    logger.error("Error while trying to get commit info: {}", e.toString())
}

jar {
    finalizedBy 'reobfJar'
    manifest {
        attributes([
                "Specification-Title"     : "socketnukes",
                "Specification-Vendor"    : "socketsmods",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "socketsmods",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit"              : commit_id,
                "Git-Commit-Timestamp"    : git_timestamp
        ] as LinkedHashMap)
    }
    includeEmptyDirs false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        mavenLocal()
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

static boolean isMainBranch(String branchName) {
    return branchName.equals("HEAD") || branchName.equals("main") || branchName.matches("\\d+.\\d+.[\\dx]+")
}

String getVersion() {
    try {
        final String branchName = grgit.branch.current().name
        boolean nonMainBranch = !isMainBranch(branchName)
        if (nonMainBranch) println("Currently on non-main branch: " + branchName)
        String ver = grgit.describe(commit: grgit.head(), longDescr: true, tags: true)
        if (ver == null) throw new RuntimeException("Could not retrieve version information from grgit")

        String[] split = ver.split("-")

        String tag = split[0]
        String commitCount = split[1]
        String commitHash = split[2]
        if (tag.startsWith("v")) tag = tag.substring(1) // Remove 'v' prefixes from tags

        boolean hasClassifier = release_type != null && !release_type.equals("release")

        String version = "${tag}.${commitCount}"
        // Append classifier if it exists
        if (hasClassifier) version += "-${release_type}"
        // Append branch and commit hash if not on main branch
        if (nonMainBranch) version += "-${commitHash}-${branchName}"

        return version
    } catch (Exception e) {
        logger.error("Error while getting version information: {}", e)
    }
    return "unknown"
}